Name        Hsync_generator;
Date        07/23/17;
Revision    1;
Device      g22v10;

/*
                               ______________
                              |   Counter    |
                      CLK x---|1           24|---x Vcc                      
                       H0 x---|2           23|---x Lstrobe                       
                       H1 x---|3           22|---x NC                       
                       H2 x---|4           21|---x Hclr                       
                       H3 x---|5           20|---x Astrobe                       
                       H9 x---|6           19|---x !Hsync                       
                       H8 x---|7           18|---x Hblank                       
                       H6 x---|8           17|---x HborderR                       
                       H7 x---|9           16|---x HborderL                       
                       H5 x---|10          15|---x NC                      
                       H4 x---|11          14|---x NC                       
                      GND x---|12          13|---x !PE                      
                              |______________|

 */

/* INPUT */

Pin 1 = CLK;
Pin 2 = H0;
Pin 3 = H1;
Pin 4 = H2;
Pin 5 = H3;
Pin 6 = H9;
Pin 7 = H8;
Pin 8 = H6;
Pin 9 = H7;
Pin 10 = H5;
Pin 11 = H4;

/* OUTPUT */

PIN 23  = Lstrobe;
PIN 16  = HborderL;
PIN 21  = Hclr;
PIN 20  = Astrobe;
PIN 19  = !Hsync;   /* HSync pulse is negative/active low */
PIN 18  = Hblank;
PIN 17  = HborderR;

/** counter starts at 0 with left of 400 display area (0000000000) **/
LeftBorder = !H0 & !H1 & !H2 & !H3 & !H4 & !H5 & !H6 & !H7 & !H8;

/** Display window starts at 64 (0000001000) **/
Window = !H0 & !H1 & !H2 & !H3 & !H4 & !H5 & H6 & !H7 & !H8;

/** The right border at 64+336 ends the visible area of the line (000010101) **/
RightBorder = !H0 & !H1 & !H2 & !H3 & H4 & !H5 & H6 & !H7 & H8;

/** The front-porch at 640 marks the start of the blanking (000010011) **/
FrontPorch = !H0 & !H1 & !H2 & !H3 & H4 & !H5 & !H6 & H7 & H8;

/** The Horizontal Sync Pulse starts at 400+20 and ends when the back-porch starts (001001011) **/
Sync = !H0 & !H1 & H2 & !H3 & !H4 & H5 & !H6 & H7 & H8;

/** The final stretch of the blanking -back-porch- starts at 400+120+64 (001001111) **/
BackPorch = !H0 & !H1 & H2 & !H3 & !H4 & H5 & H6 & H7 & H8;

/** counted till the end of the line (528) (0000100001) **/
EndOfLine = !H0 & !H1 & !H2 & !H3 & H4 & !H5 & !H6 & !H7 & !H8 & H9;

/** End of horizontal line means next line for vertical counter **/
Lstrobe = Hclr;

/** video memory address counter strobe on Pixel clock (CLK) when not in border or blank **/
Astrobe = CLK & HblankNot & HborderNotL & HborderNotR;

/** Horizontal Clear **/
HclrSet = EndOfLine;
HclrRes = LeftBorder;

HclrNot = !(Hclr # HclrSet);
Hclr = !(HclrNot # HclrRes);

/** Horizontal Sync Pulse **/
HsyncSet = Sync;
HsyncRes = BackPorch;

HsyncNot = !(Hsync # HsyncSet);
Hsync = !(HsyncNot # HsyncRes);

/** Horizontal Blank Pulse **/
HblankSet = FrontPorch;
HblankRes = EndOfLine;

HblankNot = !(Hblank # HblankSet);
Hblank = !(HblankNot # HblankRes);

/** Horizontal Border Pulse **/
HborderSetL = LeftBorder;
HborderResL = Window;

HborderNotL = !(HborderL # HborderSetL);
HborderL = !(HborderNotL # HborderResL);

HborderSetR = RightBorder;
HborderResR = FrontPorch;

HborderNotR = !(HborderR # HborderSetR);
HborderR = !(HborderNotR # HborderResR);